{-# OPTIONS --no-exact-split #-}
module ch2.Ix.DPLB where

open import Prelude
open import Foundations.Base
open Variadics _
open import Meta.Show
open import Meta.Effect hiding (_>>_) renaming (_>>=_ to _>>=áµ_)
open import Meta.Effect.Bind.State
open import Logic.Discreteness
open import System.Everything hiding (_<$>_)

open import Data.Unit
open import Data.Empty hiding (_â‰ _)
open import Data.Bool as Bool
open import Data.Reflects
open import Data.Dec as Dec
open import Data.Nat
open import Data.Nat.Order.Base
open import Data.Maybe as Maybe
open import Data.Maybe.Correspondences.Unary.Any renaming (Any to Anyâ‚˜ ; anyâ†map to anyâ†mapâ‚˜)
open import Data.Maybe.Correspondences.Unary.All renaming (All to Allâ‚˜ ; all-map to all-mapâ‚˜ ; allâ†’map to allâ†’mapâ‚˜)
open import Data.Maybe.Instances.Bind.Properties
open import Data.List as List
open import Data.List.Correspondences.Unary.All
open import Data.List.Correspondences.Unary.Any
open import Data.List.Correspondences.Unary.Unique
open import Data.List.Correspondences.Binary.OPE
open import Data.List.Correspondences.Binary.Suffix
open import Data.List.Operations.Properties as List
open import Data.List.Operations.Rel
open import Data.List.Operations.Discrete renaming (rem to remâ‚—)
open import Data.List.Instances.Map.Properties
open import Data.Sum
open import Data.String
open import Data.Fin.Inductive
open import Data.Fin.Inductive.Operations
open import Data.Fin.Inductive.Operations.Properties
open import Data.Vec.Inductive hiding (_++_) renaming (replicate to replicateáµ¥)
open import Data.Vec.Inductive.Operations hiding (_++_ ; replicate) renaming (lookup to lookupáµ¥)
open import Data.Vec.Inductive.Operations.Properties renaming (map-++ to map-++áµ¥)
open import Data.Vec.Inductive.Instances.Map

open import Order.Diagram.Meet
open import Order.Constructions.Minmax
open import Order.Constructions.Nat
open decminmax â„•-dec-total
open import Order.Constructions.Lex.Vec

open import Induction.Nat.Strong as Box using (â–¡_)
open import Induction.Nat.VLex as Boxâˆ·Ã— using (â–¡âˆ·Ã—_)

open import Data.List.NonEmpty as Listâº

open import ListSet
open import KVMapU

open import LFSet
open import LFSet.Membership
open import LFSet.Discrete as LFSet

open import ch2.Formula using (Var)
open import ch2.Sem
open import ch2.Appl
open import ch2.Ix.Formula
open import ch2.Ix.Lit
open import ch2.Ix.NF
open import ch2.Ix.CNF
open import ch2.Ix.DP
open import ch2.Ix.DPLL
open import ch2.Ix.DPTrail

private variable
  A : ğ’°
  v : Var
  Î“ Î” Î : Ctx

-- iterative + backjumping + clause learning
-- aka CDCL

BJ-ty : {Î“ : Ctx} â†’ Lit Î“ â†’ â„• â†’ ğ’°
BJ-ty {Î“} p x =
    (tr : Trail Î“)
  â†’ x ï¼ length tr
  â†’ p âˆ‰ trail-lits tr
  â†’ negate p âˆ‰ trail-lits tr
  â†’ Trail-Inv tr
  â†’ Trail-Inv2 tr
  â†’ Î£[ tr' ê‰ Trail Î“ ] (Trail-Inv tr' Ã— Trail-Inv2 tr' Ã— Backjump-suffix tr tr')

backjump-loop-backtrack : {Î“ : Ctx} â†’ CNF Î“ â†’ (p : Lit Î“)
                        â†’ âˆ€ {x}
                        â†’ (â–¡ BJ-ty p) x
                        â†’ (tr : Trail Î“)
                        â†’ x ï¼ length tr
                        â†’ p âˆ‰ trail-lits tr
                        â†’ negate p âˆ‰ trail-lits tr
                        â†’ Trail-Inv tr
                        â†’ Trail-Inv2 tr

                        â†’ (q : Lit Î“)
                        â†’ (trr : Trail Î“)
                        â†’ backtrack tr ï¼ just (q , trr)

                        â†’ Î£[ tr' ê‰ Trail Î“ ] (Trail-Inv tr' Ã— Trail-Inv2 tr' Ã— Backjump-suffix tr tr')
backjump-loop-backtrack cls p {x} ih tr e pâˆ‰ npâˆ‰ ti ti2 q trr eb =
  let (cls' , tr' , ti' , ti2' , us') = unit-propagate-iter cls ((p , guessed) âˆ· trr)
                                          (push-trailinv {tm = guessed} pâˆ‰r tir)
                                          (push-guessed-trailinv2 npâˆ‰r ti2r)
   in
  if List.has [] cls'
     then
       (let (  tr'
             , ti' , ti2' , ts') = Box.call ih prf
                                   trr
                                   refl pâˆ‰r npâˆ‰r tir ti2r
         in
          tr'
        , ti' , ti2'
        , bjsuffix-trans (bsuffixâ†’bjsuffix bsf) ts')
     else
       tr , ti , ti2 , bjsuffix-refl
  where
  bsf : Backtrack-suffix tr (q , trr)
  bsf = backtrack-suffix-eq eb
  trâŠ† : trail-lits trr âŠ† trail-lits tr
  trâŠ† = map-âŠ† fst (opeâ†’subset $ suffixâ†’ope $ suffix-uncons $ bsuffixâ†’suffix bsf)
  pâˆ‰r : p âˆ‰ trail-lits trr
  pâˆ‰r = contra trâŠ† pâˆ‰
  npâˆ‰r : negate p âˆ‰ trail-lits trr
  npâˆ‰r = contra trâŠ† npâˆ‰
  tir : Trail-Inv trr
  tir = bsuffix-trailinv bsf ti
  ti2r : Trail-Inv2 trr
  ti2r = bsuffix-trailinv2 bsf ti ti2
  prf : length trr < x
  prf = <-â‰¤-trans (<-â‰¤-trans <-ascend
                             (suffix-length $ bsuffixâ†’suffix bsf))
                  (=â†’â‰¤ (e â»Â¹))

backjump-loop : {Î“ : Ctx} â†’ CNF Î“ â†’ (p : Lit Î“)
              â†’ âˆ€[ â–¡ BJ-ty p â‡’ BJ-ty p ]
backjump-loop {Î“} cls p {x} ih tr e pâˆ‰ npâˆ‰ ti ti2 =
  Maybe.elim (Î» m â†’ backtrack tr ï¼ m
                  â†’ Î£[ tr' ê‰ Trail Î“ ] (Trail-Inv tr' Ã— Trail-Inv2 tr' Ã— Backjump-suffix tr tr'))
    (Î» _ â†’ tr , ti , ti2 , bjsuffix-refl)
    (Î» where (q , trr) â†’ backjump-loop-backtrack cls p ih tr e pâˆ‰ npâˆ‰ ti ti2 q trr)
    (backtrack tr) refl

backjump : CNF Î“
         â†’ (p : Lit Î“)
         â†’ (tr : Trail Î“)
         â†’ p âˆ‰ trail-lits tr
         â†’ negate p âˆ‰ trail-lits tr
         â†’ Trail-Inv tr â†’ Trail-Inv2 tr
         â†’ Î£[ tr' ê‰ Trail Î“ ] (Trail-Inv tr' Ã— Trail-Inv2 tr' Ã— Backjump-suffix tr tr')
backjump cls p tr pâˆ‰ npâˆ‰ ti ti2 =
  Box.fix (BJ-ty p) (backjump-loop cls p) tr refl pâˆ‰ npâˆ‰ ti ti2

DPLB-ty : {Î“ : Ctx} â†’ Vec â„• (sizeâ‚› Î“) Ã— â„• â†’ ğ’°
DPLB-ty {Î“} (x , y) =
    (cls : CNF Î“)
  â†’ (tr : Trail Î“)
  â†’ (ti : Trail-Inv tr)
  â†’ (ti2 : Trail-Inv2 tr)
  â†’ (rj : Rejstk Î“)
  â†’ Rejstk-Inv rj tr
  â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
  â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr
  â†’ Bool

dplb-loop-backjump : âˆ€ {x y}
                   â†’ (â–¡âˆ·Ã— DPLB-ty) (x , y)
                   â†’ (cls : CNF Î“)
                   â†’ (tr : Trail Î“)
                   â†’ (ti : Trail-Inv tr)
                   â†’ (ti2 : Trail-Inv2 tr)
                   â†’ (rj : Rejstk Î“)
                   â†’ Rejstk-Inv rj tr
                   â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
                   â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr

                   â†’ (p : Lit Î“)
                   â†’ (trr : Trail Î“)
                   â†’ backtrack tr ï¼ just (p , trr)

                   â†’ Bool
dplb-loop-backjump {Î“} {x} {y} ih cls tr ti ti2 rj ri ex ey p trr eb =
  Boxâˆ·Ã—.call ih
    prf
    -- computational args
    (conflict âˆ· cls)
    ((negate p , deduced) âˆ· tr')
    --
    (push-trailinv {tm = deduced} npâˆ‰' ti')
    (push-deduced-trailinv2 npâˆ‰' ti' ti2')
    (bump-at bfin p rj)
    (bump-rejstkinv-deduced {rj = rj}
       (bjsuffix-trans (bsuffixâ†’bjsuffix bsf) ts')
       cg<
       ri)
    refl refl
  where
  bsf : Backtrack-suffix tr (p , trr)
  bsf = all-unjust (subst (Î» q â†’ Allâ‚˜ (Backtrack-suffix tr) q)
                          eb
                          (backtrack-suffix {tr = tr}))

  pâˆ‰ : p âˆ‰ trail-lits trr
  pâˆ‰ = bsuffixâ†’âˆ‰ ti bsf

  npâˆ‰ : negate p âˆ‰ trail-lits trr
  npâˆ‰ = bsuffixâ†’negateâˆ‰ ti ti2 bsf

  -- computational stuff
  trti' = backjump cls p trr
            pâˆ‰ npâˆ‰
            (bsuffix-trailinv bsf ti)
            (bsuffix-trailinv2 bsf ti ti2)
  tr' = trti' .fst
  ti' = trti' .snd .fst
  ti2' = trti' .snd .snd .fst
  ts' = trti' .snd .snd .snd

  declits = filter (is-guessed? âˆ˜ snd) tr'
  conflict = insert-s (negate p) (image (negate âˆ˜ fst) declits)
  --

  npâˆ‰' : negate p âˆ‰ trail-lits tr'
  npâˆ‰' = contra (map-âŠ† fst (opeâ†’subset $ suffixâ†’ope $ bjsuffixâ†’suffix ts')) npâˆ‰

  cg< : count-guessed tr' < sizeâ‚› Î“
  cg< = <-â‰¤-trans
          (â‰¤-<-trans (ope-count (suffixâ†’ope $ bjsuffixâ†’suffix ts'))
                     (<â‰ƒsucâ‰¤ $ =â†’â‰¤ (bsuffixâ†’count-guessed bsf â»Â¹)))
          (count-guessed-size ti ti2)

  bfin : Fin (sizeâ‚› Î“)
  bfin = â„•â†’fin (count-guessed tr') cg<

  pâˆ‰r : p âˆ‰ lookupáµ¥ rj bfin
  pâˆ‰r = rejstkinv-âˆ‰ {rj = rj} bsf ts' cg< ti ti2 ri

  prf : (  map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
               (bump-at bfin p rj)
         , 2 Â· sizeâ‚› Î“ âˆ¸ suc (length tr'))
          Boxâˆ·Ã—.<âˆ·Ã— (x , y)
  prf =
    inl $
    subst (Vec-lex< _<_
                (mapáµ¥ (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                      (bump-at bfin p rj)))
                (ex â»Â¹) $
    Vec-lex<-prefix-lup bfin
      (Î» j jlt â†’
          lookup-map {xs = bump-at bfin p rj} j
        âˆ™ ap (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
             (lookup-tabulate j âˆ™ if-true (trueâ†’so! jlt))
        âˆ™ lookup-map {xs = rj} j â»Â¹) $
    â‰¤-<-trans (=â†’â‰¤ (lookup-map {xs = bump-at bfin p rj} bfin)) $
    <-â‰¤-trans
      (â‰¤-<-trans
        (=â†’â‰¤ (ap (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                          (  lookup-tabulate bfin
                           âˆ™ if-false (falseâ†’so! (<-irr {n = finâ†’â„• bfin}))
                           âˆ™ if-true (trueâ†’so! (the (finâ†’â„• bfin ï¼ finâ†’â„• bfin) refl)))))
        (<-âˆ¸-2l-â‰ƒ {m = 2 Â· sizeâ‚› Î“} {n = sizeâ‚› (p âˆ· lookupáµ¥ rj bfin)} {p = sizeâ‚› (lookupáµ¥ rj bfin)}
                           lit-set-size â»Â¹ $
         <-â‰¤-trans <-ascend $
         =â†’â‰¤ (  ap (suc âˆ˜ sizeâ‚›)
                   (rem-âˆ‰-eq pâˆ‰r â»Â¹)
              âˆ™ size-âˆ· â»Â¹)))
      (=â†’â‰¤ (lookup-map {xs = rj} bfin â»Â¹))

dplb-loop-guess : âˆ€ {x y}
                â†’ (â–¡âˆ·Ã— DPLB-ty) (x , y)
                â†’ (cls : CNF Î“)
                â†’ (tr : Trail Î“)
                â†’ (ti : Trail-Inv tr)
                â†’ (ti2 : Trail-Inv2 tr)
                â†’ (rj : Rejstk Î“)
                â†’ Rejstk-Inv rj tr
                â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
                â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr

                â†’ (cls' : CNF Î“)
                â†’ (tr'  : Trail Î“)
                â†’ Trail-Inv tr'
                â†’ Trail-Inv2 tr'
                â†’ USP-suffix tr' tr
                â†’ (ps : List (Lit Î“))
                â†’ ps â‰  []
                â†’ ps ï¼ unassigned cls tr'

                â†’ Bool
dplb-loop-guess {Î“} {x} {y} ih cls tr ti ti2 rj ri ex ey cls' tr' ti' ti2' us' ps ne eps =
  Boxâˆ·Ã—.call ih
    prf
    -- computational args
    cls
    ((p , guessed) âˆ· tr')
    --
    ti''
    (push-guessed-trailinv2 npâˆ‰ ti2')
    rj
    (push-rejstkinv-guessed {rj = rj} us' ri)
    refl refl
  where
  -- computational
  ppâˆˆ : Î£[ l ê‰ Lit Î“ ] (l âˆˆ ps)
  ppâˆˆ = posneg-rule cls' ps ne
  p = ppâˆˆ .fst
  --
  pâˆˆ = ppâˆˆ .snd
  pnpâˆ‰ : p âˆ‰ trail-lits tr' Ã— negate p âˆ‰ trail-lits tr'
  pnpâˆ‰ = unassigned-âˆ‰ {c = cls} (subst (p âˆˆ_) eps pâˆˆ)
  pâˆ‰ = pnpâˆ‰ .fst
  npâˆ‰ = pnpâˆ‰ .snd
  ti'' : Trail-Inv ((p , guessed) âˆ· tr')
  ti'' = push-trailinv {tm = guessed} pâˆ‰ ti'
  prf : (  map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
         , 2 Â· sizeâ‚› Î“ âˆ¸ suc (length tr'))
          Boxâˆ·Ã—.<âˆ·Ã— (x , y)
  prf = inr (  ex â»Â¹
             , <-â‰¤-trans
                  (<-âˆ¸-2l-â‰ƒ (trail-invâ‰¤ {tr = (p , guessed) âˆ· tr'}
                                        ti'') â»Â¹ $
                   â‰¤â‰ƒ<suc $ (uspsuffixâ†’len us'))
                  (=â†’â‰¤ (ey â»Â¹)))

dplb-loop : âˆ€[ â–¡âˆ·Ã— (DPLB-ty {Î“}) â‡’ DPLB-ty ]
dplb-loop {Î“} {x = x , y} ih cls tr ti ti2 rj ri ex ey =
  let (cls' , tr' , ti' , ti2' , us') = unit-propagate-iter cls tr ti ti2 in
  Dec.rec
    (Î» _ â†’ Maybe.elim (Î» m â†’ backtrack tr ï¼ m â†’ Bool)
              (Î» _ â†’ false)
              (Î» where (p , trr) eb â†’
                          dplb-loop-backjump ih cls tr ti ti2 rj ri ex ey p trr eb)
              (backtrack tr) refl)
    (Î» _ â†’ let ps = unassigned cls tr' in
           Dec.rec (Î» _ â†’ true)
                   (Î» ne â†’ dplb-loop-guess ih cls  tr  ti  ti2  rj ri ex ey
                                              cls' tr' ti' ti2' us' ps ne refl)
                   (Dec-is-nil? {xs = ps}))
    ([] âˆˆ? cls')

dplb : CNF Î“ â†’ Bool
dplb {Î“} c =
  Boxâˆ·Ã—.fixâˆ·Ã— DPLB-ty
    dplb-loop
    c
    []
    (emp-trailinv {Î“ = Î“}) emp-trailinv2
    (replicateáµ¥ (sizeâ‚› Î“) [])
    emp-rejstkinv
    refl
    refl

dplbsat : Formulaáµ¢ Î“ â†’ Bool
dplbsat = dplb âˆ˜ snd âˆ˜ defcnfs

dplbtaut : Formulaáµ¢ Î“ â†’ Bool
dplbtaut = not âˆ˜ dplbsat âˆ˜ Not

{-
main : Main
main =
  run $
  do -- put-str-ln $ "prime 11      : " ++â‚› (show $ tautology $ prime 11)
     -- put-str-ln $ "prime(DPLB) 13: " ++â‚› ppFBáµ¢ dplbtaut (prime 13)
     -- put-str-ln $ "prime(DPLB) 16: " ++â‚› ppFBáµ¢ dplbtaut (prime 16)
     put-str-ln $ "prime(DPLB) 21: " ++â‚› ppFBáµ¢ dplbtaut (prime 21)
-}
