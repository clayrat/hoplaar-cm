{-# OPTIONS --no-exact-split #-}
module ch2.Ix.DPLI where

open import Prelude
open import Foundations.Base
open Variadics _
open import Meta.Show
open import Meta.Effect hiding (_>>_) renaming (_>>=_ to _>>=áµ_)
open import Meta.Effect.Bind.State
open import Logic.Discreteness
open import System.Everything hiding (_<$>_)

open import Data.Unit
open import Data.Empty hiding (_â‰ _)
open import Data.Bool as Bool
open import Data.Reflects
open import Data.Dec as Dec
open import Data.Nat
open import Data.Nat.Order.Base
open import Data.Maybe as Maybe
open import Data.Maybe.Correspondences.Unary.Any renaming (Any to Anyâ‚˜ ; anyâ†map to anyâ†mapâ‚˜)
open import Data.Maybe.Correspondences.Unary.All renaming (All to Allâ‚˜ ; all-map to all-mapâ‚˜ ; allâ†’map to allâ†’mapâ‚˜)
open import Data.Maybe.Instances.Bind.Properties
open import Data.List as List
open import Data.List.Correspondences.Unary.All
open import Data.List.Correspondences.Unary.Any
open import Data.List.Correspondences.Unary.Unique
open import Data.List.Correspondences.Binary.OPE
open import Data.List.Correspondences.Binary.Suffix
open import Data.List.Operations.Properties as List
open import Data.List.Operations.Rel
open import Data.List.Operations.Discrete renaming (rem to remâ‚—)
open import Data.List.Instances.Map.Properties
open import Data.Sum
open import Data.String
open import Data.Fin.Inductive
open import Data.Fin.Inductive.Operations
open import Data.Fin.Inductive.Operations.Properties
open import Data.Vec.Inductive hiding (_++_) renaming (replicate to replicateáµ¥)
open import Data.Vec.Inductive.Operations hiding (_++_ ; replicate) renaming (lookup to lookupáµ¥)
open import Data.Vec.Inductive.Operations.Properties renaming (map-++ to map-++áµ¥)
open import Data.Vec.Inductive.Instances.Map

open import Order.Diagram.Meet
open import Order.Constructions.Minmax
open import Order.Constructions.Nat
open decminmax â„•-dec-total
open import Order.Constructions.Lex.Vec

open import Induction.Nat.Strong as Box using (â–¡_)
open import Induction.Nat.VLex as Boxâˆ·Ã— using (â–¡âˆ·Ã—_)

open import Data.List.NonEmpty as Listâº

open import ListSet
open import KVMapU

open import LFSet
open import LFSet.Membership
open import LFSet.Discrete as LFSet

open import ch2.Formula using (Var)
open import ch2.Sem
open import ch2.Appl
open import ch2.Ix.Formula
open import ch2.Ix.Lit
open import ch2.Ix.NF
open import ch2.Ix.CNF
open import ch2.Ix.DP
open import ch2.Ix.DPLL
open import ch2.Ix.DPTrail

private variable
  A : ğ’°
  v : Var
  Î“ Î” Î : Ctx

DPLI-ty : {Î“ : Ctx} â†’ Vec â„• (sizeâ‚› Î“) Ã— â„• â†’ ğ’°
DPLI-ty {Î“} (x , y) =
    (tr : Trail Î“)
  â†’ (ti : Trail-Inv tr)
  â†’ (ti2 : Trail-Inv2 tr)
  â†’ (rj : Rejstk Î“)
  â†’ Rejstk-Inv rj tr
  â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
  â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr
  â†’ Bool

dpli-loop-backtrack : âˆ€ {x y}
                    â†’ (â–¡âˆ·Ã— DPLI-ty) (x , y)
                    â†’ (tr : Trail Î“)
                    â†’ (ti : Trail-Inv tr)
                    â†’ (ti2 : Trail-Inv2 tr)
                    â†’ (rj : Rejstk Î“)
                    â†’ Rejstk-Inv rj tr
                    â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
                    â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr

                    â†’ (p : Lit Î“)
                    â†’ (trr : Trail Î“)
                    â†’ Backtrack-suffix tr (p , trr)

                    â†’ Bool
dpli-loop-backtrack {Î“} {x} {y} ih tr ti ti2 rj ri ex ey p trr bsf =
  Boxâˆ·Ã—.call ih prf
    -- computational arg
    ((negate p , deduced) âˆ· trr)
    --
    (push-trailinv {tm = deduced} npâˆ‰ ti')
    (push-deduced-trailinv2 npâˆ‰ ti' (bsuffix-trailinv2 bsf ti ti2))
    (bump-at bfin p rj)
    (bump-rejstkinv-deduced {rj = rj} (bsuffixâ†’bjsuffix bsf) cg< ri) -- TODO a version with Backtrack-suffix
    refl refl
  where
  npâˆ‰ : negate p âˆ‰ trail-lits trr
  npâˆ‰ = bsuffixâ†’negateâˆ‰ ti ti2 bsf

  bcg : count-guessed tr ï¼ suc (count-guessed trr)
  bcg = bsuffixâ†’count-guessed bsf

  cg< : count-guessed trr < sizeâ‚› Î“
  cg< = <â‰ƒsucâ‰¤ $ =â†’â‰¤ (bcg â»Â¹) âˆ™ count-guessed-size ti ti2

  bfin : Fin (sizeâ‚› Î“)
  bfin = â„•â†’fin (count-guessed trr) cg<

  pr = bsf .fst
  etr = bsf .snd .snd â»Â¹
  udptr :   Uniq (trail-pvars pr)
          Ã— Uniq (trail-pvars ((p , guessed) âˆ· trr))
          Ã— (trail-pvars pr âˆ¥ trail-pvars ((p , guessed) âˆ· trr))
  udptr = ++â†’uniq {xs = trail-pvars pr}
                  (subst Uniq
                         (trail-pvars-++ {tr1 = pr}) $
                   subst (Uniq âˆ˜ trail-pvars)
                         (etr â»Â¹)
                         ti)
  uptr = udptr .snd .fst
  dtr = udptr .snd .snd

  ti' = bsuffix-trailinv bsf ti

  pâˆ‰r : p âˆ‰ lookupáµ¥ rj bfin
  pâˆ‰r = rejstkinv-âˆ‰ {rj = rj} {tr' = trr} bsf bjsuffix-refl cg< ti ti2 ri

  prf : (  map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                (bump-at bfin p rj)
         , 2 Â· sizeâ‚› Î“ âˆ¸ suc (length trr))
          Boxâˆ·Ã—.<âˆ·Ã— (x , y)
  prf =
    (inl (subst (Vec-lex< _<_
                (mapáµ¥ (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                      (bump-at bfin p rj)))
              (ex â»Â¹) $
        Vec-lex<-prefix-lup bfin
          (Î» j jlt â†’
               lookup-map {xs = bump-at bfin p rj} j
             âˆ™ ap (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                  (  lookup-tabulate j
                   âˆ™ if-true (trueâ†’so! jlt))
             âˆ™ lookup-map {xs = rj} j â»Â¹)
          (â‰¤-<-trans
            (=â†’â‰¤ (lookup-map {xs = bump-at bfin p rj} bfin))
            (<-â‰¤-trans
               (â‰¤-<-trans
                 (=â†’â‰¤ (ap (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q)
                          (  lookup-tabulate bfin
                           âˆ™ if-false (falseâ†’so! (<-irr {n = finâ†’â„• bfin}))
                           âˆ™ if-true (trueâ†’so! (the (finâ†’â„• bfin ï¼ finâ†’â„• bfin) refl)))))
                 (<-âˆ¸-2l-â‰ƒ {m = 2 Â· sizeâ‚› Î“} {n = sizeâ‚› (p âˆ· lookupáµ¥ rj bfin)} {p = sizeâ‚› (lookupáµ¥ rj bfin)}
                           lit-set-size â»Â¹ $
                 <-â‰¤-trans <-ascend
                   (=â†’â‰¤ (  ap (suc âˆ˜ sizeâ‚›)
                              (rem-âˆ‰-eq pâˆ‰r â»Â¹)
                         âˆ™ size-âˆ· â»Â¹))))
               (=â†’â‰¤ (lookup-map {xs = rj} bfin â»Â¹))))))

dpli-loop-guess : (cls : CNF Î“)
                â†’ âˆ€ {x y}
                â†’ (â–¡âˆ·Ã— DPLI-ty) (x , y)
                â†’ (tr : Trail Î“)
                â†’ (ti : Trail-Inv tr)
                â†’ (ti2 : Trail-Inv2 tr)
                â†’ (rj : Rejstk Î“)
                â†’ Rejstk-Inv rj tr
                â†’ x ï¼ map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
                â†’ y ï¼ 2 Â· sizeâ‚› Î“ âˆ¸ length tr

                â†’ (cls' : CNF Î“)
                â†’ (tr'  : Trail Î“)
                â†’ Trail-Inv tr'
                â†’ Trail-Inv2 tr'
                â†’ USP-suffix tr' tr
                â†’ (ps : List (Lit Î“))
                â†’ ps â‰  []
                â†’ ps ï¼ unassigned cls tr'

                â†’ Bool
dpli-loop-guess {Î“} cls {x} {y} ih tr ti ti2 rj ri ex ey cls' tr' ti' ti2' us' ps ne eps =
  Boxâˆ·Ã—.call ih prf
    -- computational arg
    ((p , guessed) âˆ· tr')
    --
    ti''
    (push-guessed-trailinv2 npâˆ‰ ti2')
    rj
    (push-rejstkinv-guessed {rj = rj} us' ri)
    refl refl
  where
  -- computational
  ppâˆˆ : Î£[ l ê‰ Lit Î“ ] (l âˆˆ ps)
  ppâˆˆ = posneg-rule cls' ps ne
  p = ppâˆˆ .fst
  --
  pâˆˆ = ppâˆˆ .snd
  pnpâˆ‰ : p âˆ‰ trail-lits tr' Ã— negate p âˆ‰ trail-lits tr'
  pnpâˆ‰ = unassigned-âˆ‰ {c = cls} (subst (p âˆˆ_) eps pâˆˆ)
  pâˆ‰ = pnpâˆ‰ .fst
  npâˆ‰ = pnpâˆ‰ .snd
  ti'' : Trail-Inv ((p , guessed) âˆ· tr')
  ti'' = push-trailinv {tm = guessed} pâˆ‰ ti'

  prf : (  map (Î» q â†’ 2 Â· sizeâ‚› Î“ âˆ¸ sizeâ‚› q) rj
         , 2 Â· sizeâ‚› Î“ âˆ¸ suc (length tr'))
          Boxâˆ·Ã—.<âˆ·Ã— (x , y)
  prf = inr (  ex â»Â¹
             , <-â‰¤-trans
                  (<-âˆ¸-2l-â‰ƒ (trail-invâ‰¤ {tr = (p , guessed) âˆ· tr'}
                                        ti'') â»Â¹ $
                   â‰¤â‰ƒ<suc $ (uspsuffixâ†’len us'))
                  (=â†’â‰¤ (ey â»Â¹)))

dpli-loop : CNF Î“ â†’ âˆ€[ â–¡âˆ·Ã— (DPLI-ty {Î“}) â‡’ DPLI-ty ]
dpli-loop {Î“} cls {x = x , y} ih tr ti ti2 rj ri ex ey =
  let (cls' , tr' , ti' , ti2' , us') = unit-propagate-iter cls tr ti ti2 in
  if List.has [] cls'
    then Maybe.rec-with-âˆˆ
           (backtrack tr)
           (Î» _ â†’ false)
           (Î» where (p , trr) mb â†’
                       dpli-loop-backtrack ih tr ti ti2 rj ri ex ey p trr
                                           (backtrack-suffix-âˆˆ mb))
    else let ps = unassigned cls tr' in
         Dec.rec
           (Î» _ â†’ true)
           (Î» ne â†’ dpli-loop-guess cls ih tr  ti  ti2  rj ri ex ey
                                   cls'   tr' ti' ti2' us' ps ne refl)
           (Dec-is-nil? {xs = ps})

dpli : CNF Î“ â†’ Bool
dpli {Î“} c =
  Boxâˆ·Ã—.fixâˆ·Ã— DPLI-ty
    (dpli-loop c)
    []
    (emp-trailinv {Î“ = Î“})
    emp-trailinv2
    (replicateáµ¥ (sizeâ‚› Î“) [])
    emp-rejstkinv
    refl
    refl

dplisat : Formulaáµ¢ Î“ â†’ Bool
dplisat = dpli âˆ˜ snd âˆ˜ defcnfs

dplitaut : Formulaáµ¢ Î“ â†’ Bool
dplitaut = not âˆ˜ dplisat âˆ˜ Not

main : Main
main =
  run $
  do -- put-str-ln $ "prime 11      : " ++â‚› (show $ tautology $ prime 11)
     -- put-str-ln $ "prime(DPLI) 13: " ++â‚› ppFBáµ¢ dplitaut (prime 13)
     -- put-str-ln $ "prime(DPLI) 16: " ++â‚› ppFBáµ¢ dplitaut (prime 16)
     put-str-ln $ "prime(DPLI) 21: " ++â‚› ppFBáµ¢ dplitaut (prime 21)

